apply plugin: "maven-publish"
apply plugin: "com.jfrog.artifactory"

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      if (project.name != 'spring-js-resources') {
        artifact sourcesJar
        artifact javadocJar
      }
      versionMapping {
        usage('java-api') {
          fromResolutionOf('compileClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }
      pom {
        afterEvaluate {
          description = project.description
          name = project.description
        }
        url = "https://github.com/spring-projects/spring-webflow"
        organization {
          name = "Spring IO"
          url = "https://spring.io/projects/spring-webflow"
        }
        licenses {
          license {
            name = "The Apache Software License, Version 2.0"
            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution = "repo"
          }
        }
        scm {
          url = "https://github.com/spring-projects/spring-webflow"
          connection = "scm:git:git://github.com/spring-projects/spring-webflow"
          developerConnection = "scm:git:git://github.com/spring-projects/spring-webflow"
        }
        developers {
          developer {
            id = "rstoyanchev"
            name = "Rossen Stoyanchev"
            email = "rstoyanchev@vmware.com"
          }
        }
        issueManagement {
          system = "Jira"
          url = "https://jira.spring.io/browse/SWF"
        }
      }
    }
  }
}

artifactoryPublish {
  publications(publishing.publications.mavenJava)
}

// Disable generation of Gradle Module Metadata for compatibility
// with older versions of Spring Web Flow.
tasks.withType(GenerateModuleMetadata) {
  enabled = false
}
